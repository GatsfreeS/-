
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "РегистрНакопления.ТоварыНаСкладах";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ПеремещениеТоваровТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеТоваровТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Количество КАК Количество,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК Остаток
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							Товары.Номенклатура
	|						ИЗ
	|							Товары КАК Товары)
	|					И Склад = &Склад) КАК ТоварыНаСкладахОстатки
	|		ПО Товары.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", СкладОтправитель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Количество > Выборка.Остаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает номенклатуры: " + Выборка.Номенклатура + ", требуется: " 
								+ Выборка.Количество + ", остаток: " + Выборка.Остаток;
			Сообщение.Сообщить();
			Отказ = Истина;
		Иначе
			Движение				= Движения.ТоварыНаСкладах.Добавить();
			Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
			Движение.Период 		= Дата;
			Движение.Склад 			= СкладОтправитель;
			Движение.Номенклатура	= Выборка.Номенклатура;
			Движение.Количество 	= Выборка.Количество;
			
			Движение				= Движения.ТоварыНаСкладах.Добавить();
			Движение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
			Движение.Период 		= Дата;
			Движение.Склад 			= СкладПолучатель;
			Движение.Номенклатура	= Выборка.Номенклатура;
			Движение.Количество 	= Выборка.Количество;
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

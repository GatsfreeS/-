
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИнветаризацияТоваров") Тогда
		// Заполнение шапки
		Организация = ДанныеЗаполнения.Организация;
		Склад = ДанныеЗаполнения.Склад;
		
		Запрос = Новый Запрос;
		Запрос.Текст=
		"ВЫБРАТЬ
		|	ИнветаризацияТоваровТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ИнветаризацияТоваровТовары.КоличествоПоУчету) КАК КоличествоПоУчету,
		|	СУММА(ИнветаризацияТоваровТовары.КоличествоПоФакту) КАК КоличествоПоФакту
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	Документ.ИнветаризацияТоваров.Товары КАК ИнветаризацияТоваровТовары
		|ГДЕ
		|	ИнветаризацияТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ИнветаризацияТоваровТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	-(Товары.КоличествоПоУчету - Товары.КоличествоПоФакту) КАК Отклонение
		|ПОМЕСТИТЬ ТоварыСОтклонением
		|ИЗ
		|	Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыСОтклонением.Номенклатура КАК Номенклатура,
		|	-ТоварыСОтклонением.Отклонение КАК Количество
		|ИЗ
		|	ТоварыСОтклонением КАК ТоварыСОтклонением
		|ГДЕ
		|	ТоварыСОтклонением.Отклонение < 0";
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		
		Товары.Загрузить(Запрос.Выполнить().Выгрузить());				
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.СтоимостьТоваров.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "РегистрНакопления.ТоварыНаСкладах";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	ЭлементБлокировки = Блокировка.Добавить();
	ЭлементБлокировки.Область = "РегистрНакопления.СтоимостьТоваров";
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	СписаниеТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(СписаниеТоваровТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.СписаниеТоваров.Товары КАК СписаниеТоваровТовары
	|ГДЕ
	|	СписаниеТоваровТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеТоваровТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Количество КАК Количество,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0) КАК Остаток,
	|	ЕСТЬNULL(СтоимостьТоваровОстатки.КоличествоОстаток, 0) КАК СебестоимостьКоличество,
	|	ЕСТЬNULL(СтоимостьТоваровОстатки.СтоимостьОстаток, 0) КАК Себестоимость
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|						(ВЫБРАТЬ
	|							Товары.Номенклатура
	|						ИЗ
	|							Товары КАК Товары)
	|					И Склад = &Склад) КАК ТоварыНаСкладахОстатки
	|		ПО Товары.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьТоваров.Остатки(
	|				&МоментВремени,
	|				Номенклатура В
	|					(ВЫБРАТЬ
	|						Товары.Номенклатура
	|					ИЗ
	|						Товары КАК Товары)) КАК СтоимостьТоваровОстатки
	|		ПО Товары.Номенклатура = СтоимостьТоваровОстатки.Номенклатура";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Количество > Выборка.Остаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает номенклатуры: " + Выборка.Номенклатура + ", требуется: " 
								+ Выборка.Количество + ", остаток: " + Выборка.Остаток;
			Сообщение.Сообщить();
			Отказ = Истина;
		Иначе
			Движение				= Движения.ТоварыНаСкладах.Добавить();
			Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
			Движение.Период 		= Дата;
			Движение.Склад 			= Склад;
			Движение.Номенклатура	= Выборка.Номенклатура;
			Движение.Количество 	= Выборка.Количество;
			
			Движение				= Движения.СтоимостьТоваров.Добавить();
			Движение.ВидДвижения	= ВидДвиженияНакопления.Расход;
			Движение.Период 		= Дата;
			Движение.Номенклатура	= Выборка.Номенклатура;
			Движение.Количество		= Выборка.Количество;
			Если Выборка.СебестоимостьКоличество > Выборка.Количество Тогда
				Движение.Стоимость	= Выборка.Себестоимость / Выборка.СебестоимостьКоличество * Выборка.Количество;
			Иначе
				Движение.Стоимость	= Выборка.Себестоимость;
			КонецЕсли;			
			
		КонецЕсли;
						
	КонецЦикла;
	
КонецПроцедуры
